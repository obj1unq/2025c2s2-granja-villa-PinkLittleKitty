import wollok.game.*
import personaje.*
import cultivos.*
import bonus.*
import directions.*

describe "Personaje" {

  method initialize() {
    game.clear()
    game.addVisualCharacter(personaje)
  }

  test "Sembrar" {
    personaje.tryPlant(new Maiz())
    assert.throwsException({personaje.tryPlant(new Maiz())})
  }

  test "Regar" {
    const pos = personaje.position()
    assert.throwsException({personaje.tryWater()})
    personaje.tryPlant(new Maiz())
    personaje.tryWater()
    personaje.getCrops(pos).forEach({ c => assert.that(c.watered()) })
  }

  test "Cosechar" {
    const pos = personaje.position()
    assert.throwsException({personaje.tryHarvest()})
    const maiz = new Maiz()
    personaje.tryPlant(maiz)
    assert.throwsException({personaje.tryHarvest()})
    personaje.tryWater()
    personaje.tryHarvest()
    assert.that(personaje.inventory().contains(maiz))
    assert.that(!game.getObjectsIn(pos).contains(maiz))
  }

  test "Vender" {
    assert.that(personaje.gold() == 0)
    const maiz = new Maiz()
    personaje.tryPlant(maiz)
    personaje.tryWater()
    personaje.tryHarvest()
    assert.that(personaje.inventory().size() == 1)
    assert.throwsException({personaje.trySell()})
    game.addVisual(new Mercado(position = norte.siguiente(personaje.position())))
    personaje.position(norte.siguiente(personaje.position()))
    personaje.trySell()
    assert.that(personaje.inventory().isEmpty())
  }

  test "aspersores" {
    const aspersor = new Sprinkler()
    aspersor.position(personaje.position())
    game.addVisual(aspersor)
    const maiz1 = new Maiz()
    const maiz2 = new Maiz()
    personaje.position(sur.siguiente(personaje.position()))
    personaje.tryPlant(maiz1)
    personaje.position(norte.siguiente(personaje.position()))
    personaje.position(norte.siguiente(personaje.position()))
    personaje.tryPlant(maiz2)
    assert.that(!maiz1.watered())
    assert.that(!maiz2.watered())
    aspersor.water()
    assert.that(maiz1.watered())
    assert.that(maiz2.watered())
  }
}